<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
    <script src="csound.js"></script>
 </head>
<body>
    <script>
    var handleMessage = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + '\n' + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;        
    }
    var moduleDidLoad = function() {
        document.getElementById('console').value += 'moduleDidLoad was called; Csound functions may now be called.';
        console.warn = handleMessage;
    }
    var onPlayClick = function() {
        var csd = document.getElementById('csd').value;
        csound.compileCsdText(csd);
        csound.start();
        csound.perform();
    }
    var onPlayStop = function() {
        csound.stop();
    }
  </script>
<h1>liveconv</h1>
<p>
This example will play if your Web browser is a desktop version of Google Chrome with WebAssembly enabled. You can edit and replay the code. At this time, most but not all examples will run in WebAssembly.
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<p>
<textarea id="csd" style="width: 100%; height: 50%;font-size:12px;"><CsoundSynthesizer>
<CsOptions>
-odac  ;realtime audio out
</CsOptions>
<CsInstruments>

	sr	= 44100
	nchnls	= 2
	0dbfs	= 1

; empty IR table
giIR_record 	ftgen	0, 0, 131072, 2, 0
	
; Record impulse response
instr 13

p3 		=	ftlen(giIR_record)/sr
iskip 	=	p4
irlen 	=	p5
a1 		diskin2	"fox.wav", 1, iskip

; Fill IR table with segment from audio file
amp 	linseg	0, 0.1, 1, irlen, 1, 0.1, 0, 1, 0
andx_IR line	0, 1, 1/(ftlen(giIR_record)/sr)
		tablew	a1*amp, andx_IR, giIR_record, 1
		outch	1, a1*amp	; output the IR
ktrig 	init	1
if ktrig > -1 then
	chnset	ktrig, "conv_update"
	ktrig -= 1
endif

endin
        
; The convolver
instr 14

ain 	diskin2	"beats.wav", 1, 0, 1
kupdate chnget	"conv_update"
aconv 	liveconv ain, giIR_record, 2048, kupdate, 0
		outch	2, aconv*0.009	; output the convolution response
endin
        
        
</CsInstruments>
<CsScore>
; record impulse response
;          skip  IR_dur
i13	0	1	0.0	0.5
i13	2	1	0.5	0.5
i13	4	1	1.0	0.5
i13	6	1	1.5	0.5
i13	8	1	2.0	0.75
i13	10	1	2.38 0.25

; convolve
i14	0.0	11.65	

e

</CsScore>
</CsoundSynthesizer>

</textarea>
<h3>Csound Messages</h3>
<textarea id="console" readonly style="width: 100%; height: 25%;font-size:12px;">
</textarea>
</body>
</html>